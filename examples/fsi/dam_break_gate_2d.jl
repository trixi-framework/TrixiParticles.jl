# 2D dam break flow against an elastic plate based on
#
# P.N. Sun, D. Le Touz√©, A.-M. Zhang.
# "Study of a complex fluid-structure dam-breaking benchmark problem using a multi-phase SPH method with APR".
# In: Engineering Analysis with Boundary Elements 104 (2019), pages 240-258.
# https://doi.org/10.1016/j.enganabound.2019.03.033

using TrixiParticles
using OrdinaryDiffEq

gravity = -9.81

# ==========================================================================================
# ==== Fluid

# Note that the effect of the gate is less pronounced with lower resolutions,
# since "larger" particles don't fit through the slightly opened gate.
fluid_particle_spacing = 0.02

# Spacing ratio between fluid and boundary particles
beta_tank = 1
beta_gate = 1
tank_layers = 3
gate_layers = 3

water_width = 0.2
water_height = 0.4
water_density = 997.0

tank_width = 0.8
tank_height = 0.8

# Make the gate slightly higher than the fluid
gate_height = water_height + 4 * fluid_particle_spacing

sound_speed = 20 * sqrt(9.81 * water_height)

smoothing_length = 1.2 * fluid_particle_spacing
smoothing_kernel = SchoenbergCubicSplineKernel{2}()

state_equation = StateEquationCole(sound_speed, 7, water_density, 100000.0,
                                   background_pressure=100000.0)

viscosity = ArtificialViscosityMonaghan(0.02, 0.0)

tank = RectangularTank(fluid_particle_spacing, (water_width, water_height),
                       (tank_width, tank_height), water_density,
                       n_layers=tank_layers, spacing_ratio=beta_tank)

gate = RectangularShape(fluid_particle_spacing / beta_gate,
                        (gate_layers,
                         round(Int, gate_height / fluid_particle_spacing * beta_gate)),
                        (water_width, 0.0), water_density)

f_x(t) = 0.0
f_y(t) = -285.115t^3 + 72.305t^2 + 0.1463t
is_moving(t) = false # No moving boundaries for the relaxing step

movement = BoundaryMovement((f_x, f_y), is_moving)

# ==========================================================================================
# ==== Solid

length_beam = 0.09
thickness = 0.004
solid_density = 1161.54
n_particles_x = 5

# The structure starts at the position of the first particle and ends
# at the position of the last particle.
solid_particle_spacing = thickness / (n_particles_x - 1)

solid_smoothing_length = sqrt(2) * solid_particle_spacing
solid_smoothing_kernel = SchoenbergCubicSplineKernel{2}()

# Young's modulus and Poisson ratio
E = 3.5e6
nu = 0.45

n_particles_y = round(Int, length_beam / solid_particle_spacing) + 1

# The bottom layer is sampled separately below. Note that the `RectangularShape` puts the
# first particle half a particle spacing away from the boundary, which is correct for fluids,
# but not for solids. We therefore need to pass `tlsph=true`.
#
# The right end of the plate is 0.2 from the right end of the tank.
plate_position = 0.6 - n_particles_x * solid_particle_spacing
plate = RectangularShape(solid_particle_spacing,
                         (n_particles_x, n_particles_y - 1),
                         (plate_position, solid_particle_spacing), solid_density,
                         tlsph=true)
fixed_particles = RectangularShape(solid_particle_spacing,
                                   (n_particles_x, 1), (plate_position, 0.0),
                                   solid_density, tlsph=true)

solid = union(plate, fixed_particles)

# ==========================================================================================
# ==== Boundary models

boundary_model_tank = BoundaryModelDummyParticles(tank.boundary.density, tank.boundary.mass,
                                                  state_equation=state_equation,
                                                  AdamiPressureExtrapolation(),
                                                  smoothing_kernel, smoothing_length)

# K_tank = 9.81 * water_height
# boundary_model_tank = BoundaryModelMonaghanKajtar(K_tank, beta_tank,
#                                                   fluid_particle_spacing / beta_tank,
#                                                   tank.boundary.mass)

boundary_model_gate = BoundaryModelDummyParticles(gate.density, gate.mass,
                                                  state_equation=state_equation,
                                                  AdamiPressureExtrapolation(),
                                                  smoothing_kernel, smoothing_length)
# K_gate = 9.81 * water_height
# boundary_model_gate = BoundaryModelMonaghanKajtar(K_gate, beta_gate,
#                                                   fluid_particle_spacing / beta_gate,
#                                                   gate.mass)

hydrodynamic_densites = water_density * ones(size(solid.density))
hydrodynamic_masses = hydrodynamic_densites * solid_particle_spacing^2

k_solid = 9.81 * water_height
beta_solid = fluid_particle_spacing / solid_particle_spacing
boundary_model_solid = BoundaryModelMonaghanKajtar(k_solid, beta_solid,
                                                   solid_particle_spacing,
                                                   hydrodynamic_masses)

# `BoundaryModelDummyParticles` usually produces better results, since Monaghan-Kajtar BCs
# tend to introduce a non-physical gap between fluid and boundary.
# However, `BoundaryModelDummyParticles` can only be used when the plate thickness is
# at least two fluid particle spacings, so that the compact support is fully sampled,
# or fluid particles can penetrate the solid.
# For higher fluid resolutions, uncomment the code below for better results.
#
# boundary_model_solid = BoundaryModelDummyParticles(hydrodynamic_densites,
#                                                    hydrodynamic_masses, state_equation,
#                                                    AdamiPressureExtrapolation(),
#                                                    smoothing_kernel, smoothing_length)

# ==========================================================================================
# ==== Systems

fluid_system = WeaklyCompressibleSPHSystem(tank.fluid, ContinuityDensity(), state_equation,
                                           smoothing_kernel, smoothing_length,
                                           viscosity=viscosity, acceleration=(0.0, gravity))

boundary_system_tank = BoundarySPHSystem(tank.boundary, boundary_model_tank)

boundary_system_gate = BoundarySPHSystem(gate, boundary_model_gate,
                                         movement=movement)

solid_system = TotalLagrangianSPHSystem(solid,
                                        solid_smoothing_kernel, solid_smoothing_length,
                                        E, nu, boundary_model_solid,
                                        n_fixed_particles=n_particles_x,
                                        acceleration=(0.0, gravity))

# ==========================================================================================
# ==== Simulation

# Relaxing of the fluid without solid
semi = Semidiscretization(fluid_system, boundary_system_tank,
                          boundary_system_gate,
                          neighborhood_search=GridNeighborhoodSearch)

tspan = (0.0, 3.0)
ode = semidiscretize(semi, tspan)

info_callback = InfoCallback(interval=100)

# Use a Runge-Kutta method with automatic (error based) time step size control.
# Enable threading of the RK method for better performance on multiple threads.
# Limiting of the maximum stepsize is necessary to prevent crashing.
# When particles are approaching a wall in a uniform way, they can be advanced
# with large time steps. Close to the wall, the stepsize has to be reduced drastically.
# Sometimes, the method fails to do so with Monaghan-Kajtar BC because forces
# become extremely large when fluid particles are very close to boundary particles,
# and the time integration method interprets this as an instability.
sol = solve(ode, RDPK3SpFSAL49(),
            abstol=1e-5, # Default abstol is 1e-6 (may need to be tuned to prevent boundary penetration)
            reltol=1e-3, # Default reltol is 1e-3 (may need to be tuned to prevent boundary penetration)
            dtmax=1e-2, # Limit stepsize to prevent crashing
            save_everystep=false, callback=info_callback);

# Run full simulation
tspan = (0.0, 1.0)

is_moving(t) = t < 0.1

# Use solution of the relaxing step as initial coordinates
restart_with!(semi, sol)

semi = Semidiscretization(fluid_system, boundary_system_tank,
                          boundary_system_gate, solid_system,
                          neighborhood_search=GridNeighborhoodSearch)

ode = semidiscretize(semi, tspan)

saving_callback = SolutionSavingCallback(dt=0.02)
callbacks = CallbackSet(info_callback, saving_callback)

# See above for an explanation of the parameter choice
sol = solve(ode, RDPK3SpFSAL49(),
            abstol=1e-6, # Default abstol is 1e-6 (may need to be tuned to prevent boundary penetration)
            reltol=1e-4, # Default reltol is 1e-3 (may need to be tuned to prevent boundary penetration)
            dtmax=1e-3, # Limit stepsize to prevent crashing
            save_everystep=false, callback=callbacks);
